buildscript {

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.wiredforcode:gradle-spawn-plugin:0.8.1"
    }
}

dependencies {
    testCompile("org.codehaus.groovy:groovy-all:2.4.7")
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4')
    testCompile("org.spockframework:spock-spring:1.0-groovy-2.4")
    testCompile("cglib:cglib:3.1")
}

sourceSets {
    acceptance {
        groovy { srcDirs = ['src/acceptance/groovy'] }
        resources.srcDir file('src/acceptance/resources')
        compileClasspath += sourceSets.test.compileClasspath
        runtimeClasspath += sourceSets.test.runtimeClasspath
    }
}

test {
    testLogging {
        afterSuite { desc, result ->
            if (!desc.parent) {
                println "Test result: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} succeeded, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
            }
        }
    }
}

task runAcceptanceTests(type: Test, dependsOn: assemble) {
    description = 'Run acceptance tests'
    outputs.upToDateWhen { false }
    testClassesDir = sourceSets.acceptance.output.classesDir
    classpath = sourceSets.acceptance.runtimeClasspath
    reports.junitXml.destination = 'build/test-results'
    reports.html.destination = 'build/reports/acceptanceTests'
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "Acceptance result: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} succeeded, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
        }
    }


}

test.finalizedBy(runAcceptanceTests)

